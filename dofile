*****************************IFLS DATA CLEANING****************************

/*Purpose: To see what factors affect someone's probability to have a joint decision in their household.

[Roster] 

used to keep respondent who still alive & live in the HH */

use $hh14/bk_ar1.dta, clear
gen member14_ar=(ar01a==1 | ar01a==2| ar01a==5 | ar01a==11)
bys hhid14: egen hhsize14=sum(member14_ar)
tab hhsize14
tab ar01a member14_ar
keep if member14_ar==1

save "$output\roster14.dta", replace

/* [Dependent Variable: Decision-Makers about child's education]

1: decisions are only made by respondents
2: decisions are only made by respondents' spouses
3: decisions are taken jointly */

use $hh14/b3a_pk2.dta, clear
reshape wide pk18, i(hhid14 pid14) j(pk2type)str //reshape the data from long to wide shape

gen educexp_decision = 3 if pk18F=="AB" 
replace educexp_decision=1 if pk18F=="A" 
replace educexp_decision=2 if  pk18F=="B"
	tab educexp_decision

save $output/hh_decision14.dta, replace


/*[Independent Variable 1: Education] -> Credit: Firman Witoelar */

use $hh14/b3a_dl1.dta, clear
des
order hhid14 pid14 dl04 dl06 dl07
keep  hhid14 pid14 dl04 dl06 dl07
tab dl04,m
tab dl06,m
tab dl07,m
generate education=0       if dl06==1 | dl06==90 | dl06==. | dl07==. | (dl06==98 & dl07==98)
replace  education=dl07    if (dl06==2 | dl06==11 | dl06==14 | dl06==72) & dl07<7
replace  education=6       if (dl06==2 | dl06==11 | dl06==14 | dl06==72) & dl07==7 ///
                              | (dl06==95 & dl07==7) | dl06==98 & dl07==7
replace  education=5       if (dl06==2 | dl06==11 | dl06==14 | dl06==72) & dl07>7
replace  education=6+dl07  if (dl06==3 | dl06==4 | dl06==12 | dl06==17 | dl06==73) ///
                               & dl07<4
replace  education=9       if (dl06==3 | dl06==4 | dl06==12 | dl06==17 | dl06==73) /// 
                               & (dl07==4 | dl07==5 | dl07==7)
replace  education=8       if (dl06==3 | dl06==4 | dl06==12 | dl06==17 | dl06==73) /// 
                               & dl07>7
replace  education=9+dl07  if (dl06==5 | dl06==6 | dl06==15 | dl06==74) & dl07<4
replace  education=12      if (dl06==5 | dl06==6 | dl06==15 | dl06==74) & ///
                              (dl07==4|dl07==7)
replace  education=11      if (dl06==5 | dl06==6 | dl06==15 | dl06==74) & dl07==98
replace  education=12+dl07 if (dl06==13 | dl06==60 | dl06==61 | dl06==62 | dl06==63) /// 
                               & dl07<5
replace  education=16      if (dl06==13 | dl06==60 | dl06==61 | dl06==62 | dl06==63) /// 
                               & (dl07==5 | dl07==6 | dl07==7)
replace education=15       if (dl06==13 | dl06==60 | dl06==61 | dl06==62 | dl06==63)& dl07>7
replace education=0 if education==. 

gen     kat_educ=1 if education==0 
replace kat_educ=2 if education>=1 & education<7
replace kat_educ=3 if education>=7 & education<=12
replace kat_educ=4 if education>12

order hhid14 pid14 education kat_educ 
keep  hhid14 pid14 education kat_educ 
sum education kat_educ
count
sort hhid14 pid14 
save "$output/educ14.dta", replace


/*[Independent Variable 2: Sex]*/

use $hh14/bk_ar1.dta, clear
ren ar07 male
recode male (3=0)
label define male 1"male" 0"female"
label values male male
keep pid14 hhid14 male
order hhid14 pid14

save $output/male14.dta, replace

/*[Independent Variable 3: Residence] */

use $hh14/bk_sc1.dta, clear
ren sc05 urban
recode urban (2=0)
label define urban 1"urban" 0"rural"
label values urban urban
keep hhid14 urban
tab urban
save $output/urban14.dta, replace


/*[Independent Variable 4: Generation]

Gen Z = 3-18 years old
Gen Y = 19-29 years old
Gen X = 35-50 years old
Baby Boomers = 51-69 years old */

use $hh14/bk_ar1.dta, clear
recode ar09 (998/999=.)
ren ar09 age

gen agesq = age^2

gen kat_age = 1 if age>=3 & age<=18
replace kat_age=2 if age>=19 & age<=29
replace kat_age=3 if age>=35 & age<=50
replace kat_age=4 if age>=51 & age<=69

la def kat_age 1"gen z" 2"gen y" 3"gen x" 4"baby boomers"
la val kat_age kat_age

tab kat_age

save $output/kat_age14.dta, replace

/* [Independent Variable 4: Working-status based on BPS definition of working] */

use $hh14/b3a_tk1.dta, clear
recode tk02 tk03 (8=.)
gen employ = (tk01==1 | tk02==1 | tk03==1 | tk04==1)
label define employ 1"employed" 0"unemployed"
label values employ employ
tab employ

save $output/emp14.dta, replace

/* [Independent Variable 5: Per capita Expenditure] */

use $pce/pce14nom.dta, clear
gen lpce=log(pce)
save "$output/pce14.dta", replace

/* [Independent Variable 6: Length of Marriage] -> Credit: Amira Tanaya, modified by me */ 

use "$hh14/b3a_kw1.dta", clear
tab kw02jyr
ren kw02jyr yrmarr
drop if yrmarr==9998

tab yrmarr

gen tmarr=2015-yrmarr
tab tmarr

save $output/tmarr14_3a, replace

use "$hh14/b4_kw2.dta", clear
tab kw02iyr   
ren kw02iyr yrmarr
drop if yrmarr==9998

tab yrmarr, m

gen tmarr=2015-yrmarr
tab tmarr

append using $output/tmarr14_3a, force 

save $output/tmarr14, replace

/*****merging

1:1: merge individual data to individual data
1:m: merge individual data to household data
m:1: merge household data to individual data */

use $output/roster14, clear //use roster as master data because i want to make sure that respondents are still alive & live in the HH

merge 1:1 hhid14 pid14 using $output/hh_decision14
drop if _merge!=3
drop _merge

merge 1:1 hhid14 pid14 using $output/educ14
drop if _merge!=3
drop _merge

merge 1:1 hhid14 pid14 using $output/male14
drop if _merge!=3
drop _merge

merge 1:1 hhid14 pid14 using $output/kat_age14
drop if _merge!=3
drop _merge

merge 1:1 hhid14 pid14 using $output/emp14
drop if _merge!=3
drop _merge

merge 1:m hhid14 pid14 using $output/tmarr14
drop if _merge!=3
drop _merge

merge m:1 hhid14 using $output/pce14
drop if _merge!=3
drop _merge

merge m:1 hhid14 using $output/urban14
drop if _merge!=3
drop _merge


/*****regression & outreg

using multinomial logit regression cause the outcome more than 2 and the outcome are not ordinal

outreg is allows us to output our regression results to other file formats, such as ms. word. */

ssc install outreg2

mlogit educexp_decision education male i.kat_age employ lpce tmarr urban
margins, dydx(*) atmeans predict (pr outcome (1)) post
outreg2 using ifls_cleaning.doc, replace

mlogit educexp_decision education male i.kat_age employ lpce tmarr urban
margins, dydx(*) atmeans predict (pr outcome (2)) post
outreg2 using ifls_cleaning.doc, append

mlogit educexp_decision education male i.kat_age employ lpce tmarr urban
margins, dydx(*) atmeans predict (pr outcome (3)) post
outreg2 using ifls_cleaning.doc, append


----------------------------------------------------------------------END----------------------------------------------------------------



